buildscript {
    repositories {
        mavenCentral()
    }
}


if (!project.hasProperty("NEXUS_USERNAME")) {
    println "No NEXUS_USERNAME found"
    ext.NEXUS_USERNAME = ""
}

if (!project.hasProperty("NEXUS_PASSWORD")) {
    println "No NEXUS_PASSWORD found"
    ext.NEXUS_PASSWORD = ""
}

allprojects {

    apply plugin: 'maven'
    apply plugin: 'signing'   
}


allprojects {

    apply plugin: "java"
    apply plugin: "groovy"

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    version = VERSION_NAME
    group = GROUP

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: "$NEXUS_USERNAME", password: "$NEXUS_PASSWORD")
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: "$NEXUS_USERNAME", password: "$NEXUS_PASSWORD")
                }

                pom.project {
                    name 'Metrics client for Java'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'An application metrics client for Java integrated with Graphite/OpenTSDB'
                    url 'https://github.com/adobe/metrics-client-for-java'

                    scm {
                      connection 'scm:git:https://github.com/adobe/metrics-client-for-java.git'
                      developerConnection 'scm:git:git@github.com/adobe/metrics-client-for-java.git'
                      url 'https://github.com/adobe/metrics-client-for-java.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'costimuraru'
                            name 'Constantin Muraru'
                        }
                    }
                }
            }
        }
    }
}

subprojects {
    
    // Base version for all modules
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        compile 'com.typesafe:config:1.3.2'
        compile group: 'com.google.guava', name: 'guava', version: '16.0'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.1'

        // fail-safe via circuit breakers along retry policies
        compile group: 'net.jodah', name: 'failsafe', version: '1.0.3'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.4'

        compileOnly "org.immutables:value:2.5.5:annotations"
        annotationProcessor "org.immutables:value:2.5.5"

        testCompile 'junit:junit:4.11'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile "org.spockframework:spock-core:1.1-groovy-2.4"
        testCompile 'org.mockito:mockito-core:1.9.5'
        testCompile 'org.powermock:powermock-mockito-release-full:1.6.4'
        testCompile 'net.bytebuddy:byte-buddy:1.8.11'
    }
}

configurations.all {
    resolutionStrategy.force "org.slf4j:slf4j-api:${slf4jVersion}"
}

configurations {
    all *.exclude module: 'xmlParserAPIs'
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}